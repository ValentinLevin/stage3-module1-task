plugins {
    id 'java'
    id 'idea'
}

wrapper {
    gradleVersion = gradlewVersion
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    group = 'com.mjc.school'
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    idea {
        project {
            languageLevel = 17
        }

        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        compileOnly "org.projectlombok:lombok:${lombokVersion}"

        implementation "org.hibernate.validator:hibernate-validator:${hibernateValidatorVersion}"
        annotationProcessor "org.hibernate.validator:hibernate-validator-annotation-processor:${hibernateValidatorVersion}"
        implementation "org.glassfish.expressly:expressly:${expresslyVersion}"

        implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"

        implementation "jakarta.servlet:jakarta.servlet-api:${servletVersion}"

        implementation "org.slf4j:slf4j-api:${slf4jVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
        testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    }

    test {
        useJUnitPlatform()
    }

    configurations.configureEach {
        resolutionStrategy {
            failOnVersionConflict()
            force("org.slf4j:slf4j-api:${slf4jVersion}")
            force("net.bytebuddy:byte-buddy:1.14.12")
        }
    }
}

task repack(type: Jar, dependsOn: ':module-main:build') {
    if (findProject(':module-main') != null) {
        entryCompression = ZipEntryCompression.STORED
        def buildJar = zipTree("${project(':module-main').buildDir}/libs/module-main-all.jar")
        from(buildJar) { into('/') }
        manifest {
            attributes(
                    'Main-Class': 'com.mjc.school.Main'
            )
        }
    }
}
